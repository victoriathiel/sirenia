# FBD dating

# exponential priors on the speciation and extinction rate parameters
speciation_rate ~ dnExponential(10)
extinction_rate ~ dnExponential(10)
fossil_samp_rate ~ dnExponential(10)

# moves on speciation and extinction
moves.append( mvScale(speciation_rate, lambda = 0.5, tune = true, weight = 3.0) )
moves.append( mvScale(extinction_rate, lambda = 0.5, tune = true, weight = 3.0) )
moves.append( mvScale(fossil_samp_rate, lambda = 0.5, tune = true, weight = 3.0) )

diversification := speciation_rate - extinction_rate
turnover := extinction_rate/speciation_rate

# extant species sampling
rho <- 0.12

# prior and move on the origin
origin_time ~ dnUnif(59.2, 100.0)
moves.append( mvSlide(origin_time, weight=1.0) )

# birth death tree prior
tree ~ dnFBDP(lambda = speciation_rate, mu = extinction_rate, psi = fossil_samp_rate, rho = rho, origin = origin_time, taxa = taxa)

# moves to search tree space
moves.append( mvFNPR(tree, weight = 15.0) )
moves.append( mvCollapseExpandFossilBranch(tree, origin_time, weight = 6.0) )
moves.append( mvNodeTimeSlideUniform(tree, weight = 40.0) )
moves.append( mvRootTimeSlideUniform(tree, origin_time, weight = 5.0) )


clade_extant = clade("Proboscidea","Trichechus_hesperamazonicus","Trichechus_inunguis",
                    "Hydrodamalis_gigas","Dugong_dugon")
age_extant := tmrca(tree, clade_extant)

num_samp_anc := tree.numSampledAncestors()

fossils = tree.getFossils()

for(i in 1:fossils.size())
{
    t[i] := tmrca(tree, clade(fossils[i]))

    a_i = fossils[i].getMinAge()
    b_i = fossils[i].getMaxAge()

    F[i] ~ dnUniform(t[i] - b_i, t[i] - a_i)
    F[i].clamp( 0 )
}

# moves on fossil ages
moves.append( mvFossilTimeSlideUniform(tree, origin_time, weight = 5.0) )
